@get_thread[]
^dbconnect{
	$thread[^table::sql{SELECT * FROM threads WHERE id = $form:fields.thread}]
}

@setup[]
$setup_worked[0]
^if(def $form:fields.board && def $form:fields.thread){
	^get_board[]
	^get_thread[]
	^if($board > 0 && $thread > 0){
		$setup_worked[1]
	}
}

@execute_form[]
^execute_reply_form[]

@title[]
/$board.uri/ - $thread.title - $board.name 

@body[]
^display_thread[$thread]
<hr>
<center>^new_reply[]</center>

@new_reply[]
<h1>Post a reply</h1>
^reply_form[]

@reply_form[]
<form method="POST" enctype="multipart/form-data">
<table>
	<tbody>
		<tr>
			<td>Name</td>
			<td><input name="author" placeholder="Anonymous"></td>
		</tr>
		<tr>
			<td>Body</td>
			<td colspan="2"><textarea name="comment"></textarea></td>
		</tr>
		<tr>
			<td>File</td>
			<td><input type="file" name="image" /></td>
		</tr>
		<tr>
			<td><input type="submit" value="Post" name="posted"></td>
		</tr>
	</tbody>
</table>
</form>

@execute_reply_form[]
^if(def $form:comment){
	^if(def $form:image){
		^try{
			$image[^image::measure[$form:image]]
			^dbconnect{
				$id[^int:sql{CALL new_reply(
					'$form:comment',
					^if(def $form:author){'$form:author'}{'Anonymous'},
					$thread.id,
					'$form:image.name'
				)}]
			}
			^form:image.save[binary;/images/${id}.^file:justext[$form:image.name]]
			$f[^file::exec[/compress.sh;;${id}.^file:justext[$form:image.name];${id}c.jpg;125x125>]]
		}{
			$exception.handled(true)
			<h1>Image upload failed.</h1>
		}
	}{
		^dbconnect{
			^void:sql{CALL new_reply(
				'$form:comment',
				^if(def $form:author){'$form:author'}{'Anonymous'},
				$thread.id,
				NULL
			)}
		}
	}
}